package org.fosstrak.capturingapp
 
import org.fosstrak.capturingapp.util.Util; 
import org.fosstrak.ale.xsd.ale.epcglobal.ECReport;
import org.fosstrak.ale.xsd.ale.epcglobal.ECReports;
import org.fosstrak.ale.xsd.epcglobal.EPC;
import org.fosstrak.capturingapp.util.SimpleEPCISDocument;
import org.fosstrak.epcis.model.ActionType;

import java.util.LinkedList;
import function org.fosstrak.capturingapp.util.Util.extractEPC;

// the global collector for all the EPCIS documents for further processing.
global java.util.List epcisResults

rule "ADDITIONS Rule Tags from reader 'Reader_Shelve1' from the specName 'ECSpec_additions'"
	dialect "java"
	when
		$reports : ECReports(reports != null, specName == "ECSpec_additions")
		$epcs : LinkedList( size > 0 ) from collect (
			EPC() from extractEPC(Util.selectTag, $reports)
			) 
	then
	
		SimpleEPCISDocument simpleDocument = new SimpleEPCISDocument();
		simpleDocument.addObjectEvent(
			$epcs, 
			ActionType.OBSERVE, 
			"urn:epcglobal:cbv:bizstep:storing", 
			"urn:epcglobal:cbv:disp:sellable_not_accessible",
			"urn:epc:id:sgln:76300544.00000.1",
			"urn:epc:id:sgln:76300544.00000.0"
			);
		
		System.out.println("\n=====================================================");
		System.out.println("Additions tags seen:");
		for (Object o : $epcs) System.out.println(((EPC)o).getValue());
		System.out.println("=====================================================\n");

		epcisResults.add(simpleDocument.getDocument());
end

rule "DELETIONS Rule Tags from reader 'Reader_Shelve1' from the specName 'ECSpec_deletions'"
	dialect "java"
	when
		$reports : ECReports(reports != null, specName == "ECSpec_deletions")
		$epcs : LinkedList( size > 0 ) from collect (
			EPC() from extractEPC(Util.selectTag, $reports)
			) 
	then
	
		SimpleEPCISDocument simpleDocument = new SimpleEPCISDocument();
		simpleDocument.addObjectEvent(
			$epcs, 
			ActionType.OBSERVE, 
			"urn:epcglobal:cbv:bizstep:unpacking",
			"urn:epcglobal:cbv:disp:partially_dispensed",
			"urn:epc:id:sgln:76300544.00000.1",
			"urn:epc:id:sgln:76300544.00000.0"
			);
		System.out.println("\n=====================================================");
		System.out.println("Deletions tags seen:");
		for (Object o : $epcs) System.out.println(((EPC)o).getValue());
		System.out.println("=====================================================\n");

		epcisResults.add(simpleDocument.getDocument());
end